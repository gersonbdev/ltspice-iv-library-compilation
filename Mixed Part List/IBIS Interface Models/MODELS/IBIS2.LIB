**********
IBIS2 - IBIS models using Intusoft SPICE 2G Compatible Topology(8): 
These parts use the Intusoft SPICE 2G compatible subcircuit topology.
They can be used with any SPICE 2G compatible simulator including most
popular PC based simulators.

All output parts connect in the same way: Input, Output, VCC, VEE, Enable
All input parts connect in the same way: Input, VCC, VEE
Suffix Meanings - O = Output, I = Input, B = BEST CASE, W = WORST CASE, 
T = TYPICAL

        Input Voltage    Supply Voltage   Enable Voltage  Program Required
IBIS1     Rail-Rail         Rail-Rail       Rail-Rail      IsSpice3
IBIS2     0 - 1V            Rail-Rail       0 - 1V         SPICE 2G.6
IBIS3     Rail-Rail         Rail-Rail       Rail-Rail      IsSpice3

Note: IBIS level 2 REQUIRES 0 - 1V levels for both the input and enable
lines.

To use these models simply call the part as usual and change the 
default library call from 

"*INCLUDE IBIS1.LIB" to "*INCLUDE IBIS2.LIB"

Other Libraries
IBIS1: IBIS level 1 subcircuit, requires IsSpice3
IBIS1A: IBIS level 1 subcircuit, requires IsSpice3
IBIS3: IBIS level 3 subcircuit, requires IsSpice3
*
** Pre-canned, ready to use models are listed below
Component: Any PCI Shared Component
PCIOB Buffer - emulates signals: A0-A31, DRVPCI
PCIOW Buffer - emulates signals: A0-A31, DRVPCI
PCIIB Buffer  - emulates signals: A0-A31, DRVPCI
PCIIW Buffer  - emulates signals: A0-A31, DRVPCI
Component: PCMC
PCMC1OB Buffer   - emulates signals: A0-A31, DRVPCI
PCMC1IB Buffer   - emulates signals: A0-A31, DRVPCI
PCMC1OW Buffer   - emulates signals: A0-A31, DRVPCI
PCMC1IW Buffer   - emulates signals: A0-A31, DRVPCI
*
***********
.SUBCKT PCIOW   16    15     9   5   13
*Connections    Input Output VCC VEE Enable 
*Input/Enable Control
X1 8 2 INV
X6 6 3 INV
X9 16 7 INV
*
X3 1 5 8 NSWITCH
X4 9 4 6 SWITCH
X5 4 5 3 NSWITCH
X7 13 8 16 SWITCH
X8 13 6 7 SWITCH
X10 9 1 2 SWITCH
R3 9 17 33K
R4 17 5 33K
C3 4 5 1P
C4 9 1 1P
* Pull-up/pull-down structures Min
M2 17 4 5 5 PCIOUTLN
M3 17 1 9 9 PCIOUTHN
.MODEL PCIOUTLN NMOS (LEVEL=1 VTO=2.2 KP=36M GAMMA=1.48
+ LAMBDA=50.49M RD=1.345 RS=2.95 IS=100F CGDO=12N)
.MODEL PCIOUTHN PMOS (LEVEL=1 VTO=-2.2 KP=21.1M GAMMA=1.48
+ LAMBDA=237.49M RD=4.8 RS=13.2 IS=75F CGDO=12N)
* Diode Clamps
D1 17 9 DIODE
D2 5 17 DIODE
.MODEL DIODE D IS=13N N=1.8 RS=14
* Package Parasitics Max
ROSNB 10 14 100
ROPKG 14 15 .200
CCOMP 10 5 8.00P
LOPKG 10 14 15.00N
COPKG 15 5 2.00P
V5 17 10 
.ENDS
.SUBCKT INV 1 2
* LOAD SET TO MAKE 0 AND 1 WITHIN 1PPM OF DESIRED VALUE
RIN1 1 0 3E8
E1 2 0  1 0 1 -1
.ENDS
.SUBCKT SWITCH 1 2 3
R1 1 2 3E13
G1 1 2 POLY(2) 1 2 3 0 0 0 0 0 3.125M
* 320 OHM ON, 3E13 OFF
.ENDS
.SUBCKT NSWITCH 1 2 3
R1 1 2 3E13
G1 1 2 POLY(2) 1 2 3 0 0 0 0 0 3.125M
* 320 OHM ON, 3E13 OFF
.ENDS
***********
.SUBCKT PCIIW   15    9   5
*Connections    Input VCC VEE
D1 17 9 DIODE
D2 5 17 DIODE
.MODEL DIODE D IS=13N N=1.8 RS=14
* Package Parasitics Max
ROSNB 10 14 100
ROPKG 14 15 .200
CCOMP 10 5 8.00P
LOPKG 10 14 15.00N
COPKG 15 5 2.00P
V5 17 10 
.ENDS
***********
.SUBCKT PCIOB   16    15     9   5   13
*Connections    Input Output VCC VEE Enable 
*Input/Enable Control
X1 8 2 INV
X6 6 3 INV
X9 16 7 INV
*
X3 1 5 8 NSWITCH
X4 9 4 6 SWITCH
X5 4 5 3 NSWITCH
X7 13 8 16 SWITCH
X8 13 6 7 SWITCH
X10 9 1 2 SWITCH
R3 9 17 33K
R4 17 5 33K
C3 4 5 1P
C4 9 1 1P
* Pull-up/pull-down structures Max
M2 17 4 5 5 PCIOUTLX
M3 17 1 9 9 PCIOUTHX
.MODEL PCIOUTLX NMOS (LEVEL=1 VTO=2.2 KP=85M GAMMA=1.48
+ LAMBDA=22.49M RD=405M RS=1.71 IS=100F CGDO=12N)
.MODEL PCIOUTHX PMOS (LEVEL=1 VTO=-2.2 KP=101.5M GAMMA=1.48
+ LAMBDA=287.49M RD=1.15 RS=8.13 IS=75F CGDO=12N)
*
* Diode Clamps
D1 17 9 DIODE
D2 5 17 DIODE
.MODEL DIODE D IS=13N N=1.8 RS=14
* Package Parasitics Min
ROSNB 10 14 100
ROPKG 14 15 .100
CCOMP 10 5 4.00P
LOPKG 10 14 8.00N
COPKG 15 5 1.00P
V5 17 10 
.ENDS
.SUBCKT INV 1 2
* LOAD SET TO MAKE 0 AND 1 WITHIN 1PPM OF DESIRED VALUE
RIN1 1 0 3E8
E1 2 0  1 0 1 -1
.ENDS
.SUBCKT SWITCH 1 2 3
R1 1 2 3E13
G1 1 2 POLY(2) 1 2 3 0 0 0 0 0 6.25M
* 160 OHM ON, 3E13 OFF
.ENDS
.SUBCKT NSWITCH 1 2 3
R1 1 2 3E13
G1 1 2 POLY(2) 1 2 3 0 0 0 0 0 6.25M
* 160 OHM ON, 3E13 OFF
.ENDS
***********
.SUBCKT PCIIB   15    9   5
*Connections    Input VCC VEE
D1 17 9 DIODE
D2 5 17 DIODE
.MODEL DIODE D IS=13N N=1.8 RS=14
* Package Parasitics
ROSNB 10 14 100
ROPKG 14 15 .100
CCOMP 10 5 4.00P
LOPKG 10 14 8.00N
COPKG 15 5 1.00P
V5 17 10 
.ENDS
***********
.SUBCKT PCMC1OW   16    15     9   5   13
*Connections      Input Output VCC VEE Enable 
*Input/Enable Control
X1 8 2 INV
X6 6 3 INV
X9 16 7 INV
*
X3 1 5 8 NSWITCH
X4 9 4 6 SWITCH
X5 4 5 3 NSWITCH
X7 13 8 16 SWITCH
X8 13 6 7 SWITCH
X10 9 1 2 SWITCH
R3 9 17 33K
R4 17 5 33K
C3 4 5 1P
C4 9 1 1P
* Pull-up/pull-down structures Min
M2 17 4 5 5 PCIOUTLN
M3 17 1 9 9 PCIOUTHN
.MODEL PCIOUTLN NMOS (LEVEL=1 VTO=2.2 KP=12M GAMMA=1.48
+ LAMBDA=12M RD=1.445 RS=7.74 IS=100F CGDO=12N)
.MODEL PCIOUTHN PMOS (LEVEL=1 VTO=-2.2 KP=8.6M GAMMA=1.48
+ LAMBDA=76.23M RD=7.1 RS=10.2 IS=75F CGDO=12N)
* Diode Clamps
D1 17 9 DIODEV
D2 5 17 DIODEG
.MODEL DIODEV D IS=45N N=1.98 RS=938.6M
.MODEL DIODEG D IS=.9N N=1.51 RS=1.165
* Package Parasitics Max
ROSNB 10 14 100
ROPKG 14 15 .277
CCOMP 10 5 7.64P
LOPKG 10 14 32.75N
COPKG 15 5 1.36P
V5 17 10 
.ENDS
.SUBCKT INV 1 2
* LOAD SET TO MAKE 0 AND 1 WITHIN 1PPM OF DESIRED VALUE
RIN1 1 0 3E8
E1 2 0  1 0 1 -1
.ENDS
.SUBCKT SWITCH 1 2 3
R1 1 2 3E13
G1 1 2 POLY(2) 1 2 3 0 0 0 0 0 1.48M
* 675 OHM ON, 3E13 OFF
.ENDS
.SUBCKT NSWITCH 1 2 3
R1 1 2 3E13
G1 1 2 POLY(2) 1 2 3 0 0 0 0 0 1.80M
* 556 OHM ON, 3E13 OFF
.ENDS
***********
.SUBCKT PCMC1IW   15    9   5
*Connections    Input VCC VEE
D1 17 9 DIODEV
D2 5 17 DIODEG
.MODEL DIODEV D IS=45N N=1.98 RS=938.6M
.MODEL DIODEG D IS=.9N N=1.51 RS=1.165
* Package Parasitics Max
ROSNB 10 14 100
ROPKG 14 15 .277
CCOMP 10 5 7.64P
LOPKG 10 14 32.75N
COPKG 15 5 1.36P
V5 17 10 
.ENDS
***********
.SUBCKT PCMC1OB   16    15     9   5   13
*Connections      Input Output VCC VEE Enable 
*Input/Enable Control
X1 8 2 INV
X6 6 3 INV
X9 16 7 INV
*
X3 1 5 8 NSWITCH
X4 9 4 6 SWITCH
X5 4 5 3 NSWITCH
X7 13 8 16 SWITCH
X8 13 6 7 SWITCH
X10 9 1 2 SWITCH
R3 9 17 33K
R4 17 5 33K
C3 4 5 1P
C4 9 1 1P
* Pull-up/pull-down structures Max
M2 17 4 5 5 PCIOUTLX
M3 17 1 9 9 PCIOUTHX
.MODEL PCIOUTLX NMOS (LEVEL=1 VTO=2.2 KP=28M GAMMA=1.48
+ LAMBDA=25.49M RD=1.4 RS=7.24 IS=100F CGDO=12N)
.MODEL PCIOUTHX PMOS (LEVEL=1 VTO=-2.2 KP=33.7M GAMMA=1.48
+ LAMBDA=233.41M RD=9.25 RS=11.13 IS=75F CGDO=12N)
*
* Diode Clamps
D1 17 9 DIODEV
D2 5 17 DIODEG
.MODEL DIODEV D IS=45N N=1.98 RS=938.6M
.MODEL DIODEG D IS=.9N N=1.51 RS=1.165
* Package Parasitics Min
ROSNB 10 14 100
ROPKG 14 15 .227
CCOMP 10 5 2.79P
LOPKG 10 14 26.75N
COPKG 15 5 1.21P
V5 17 10 
.ENDS
.SUBCKT INV 1 2
* LOAD SET TO MAKE 0 AND 1 WITHIN 1PPM OF DESIRED VALUE
RIN1 1 0 3E8
E1 2 0  1 0 1 -1
.ENDS
.SUBCKT SWITCH 1 2 3
R1 1 2 3E13
G1 1 2 POLY(2) 1 2 3 0 0 0 0 0 5.29M
* 189 OHM ON, 3E13 OFF
.ENDS
.SUBCKT NSWITCH 1 2 3
R1 1 2 3E13
G1 1 2 POLY(2) 1 2 3 0 0 0 0 0 6.98M
* 143 OHM ON, 3E13 OFF
.ENDS
***********
.SUBCKT PCMC1IB   15    9   5
*Connections    Input VCC VEE
D1 17 9 DIODEV
D2 5 17 DIODEG
.MODEL DIODEV D IS=45N N=1.98 RS=938.6M
.MODEL DIODEG D IS=.9N N=1.51 RS=1.165
* Package Parasitics
ROSNB 10 14 100
ROPKG 14 15 .227
CCOMP 10 5 2.79P
LOPKG 10 14 26.75N
COPKG 15 5 1.21P
V5 17 10 
.ENDS
**********
*SYM=SWITCH
.SUBCKT SWITCH 1 2 3
R1 1 2 3E13
G1 1 2 POLY(2) 1 2 3 0 0 0 0 0 5M
* 200 OHM ON, 3E13 OFF
.ENDS
**********
*SYM=SWITCH
.SUBCKT NSWITCH 1 2 3
R1 1 2 3E13
G1 1 2 POLY(2) 1 2 3 0 0 0 0 0 6.67M
* 150 OHM ON, 3E13 OFF
.ENDS
***********
*SYM=BLINE
.SUBCKT BLINE1 1 2 {ER0=8.85P}
*Interconnect Model for the Suface Microstrip Stub from the 
* PCI Speedway to the Load
O1 1 0 2 0 LOSSY
.MODEL LOSSY LTRA rel=1.8 len={LENGTH*.0254}M
+ r=0 g=0
+ l=523NH/m
+ c=76PF/m 
* z0 = {(523N/76P)^.5}Ohms
* td = {(523N*76P)^.5}s
* Total Delay = {(523N*76P)^.5 * (LENGTH * .0254)}s
.ENDS
***********
*SYM=BLINE
.SUBCKT BLINE 1 2 {ER0=8.85P}
*Interconnect Model for the Buried Microstrip PCI Speedway line
O1 1 0 2 0 LOSSY
.MODEL LOSSY LTRA rel=1.8 len={LENGTH*.0254}M
+ r=0 g=0
+ l=723NH/m
+ c=76PF/m 
* z0 = {(723N/76P)^.5}Ohms 
* td = {(723N*76P)^.5}s
* Total Delay = {(723N*76P)^.5 * (LENGTH * .0254)}s
.ENDS
***********