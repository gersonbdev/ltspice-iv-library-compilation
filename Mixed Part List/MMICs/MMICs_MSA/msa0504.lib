*SPICE model for MSA0504 
*Start Package model 
.SUBCKT MSA0504   OUT   IN   GND
* PI NETWORK TO SIMULATE TRANSMISSION LINE T1
C1T1    11      0       .189PF
LT1     11      12      1.083NH
C2T1    12      0       .189PF
* PI NETWORK TO SIMULATE TRANSMISSION LINE T2
C1T2    14      0       .153PF
LT2     14      15      .190NH
C2T2    15      0       .153PF
* PI NETWORK TO SIMULATE TRANSMISSION LINE T3
C1T3    13      0       .140PF
LT3     13      DIEOUT  .824NH
C2T3    DIEOUT  0       .140PF
* Bond wire inductance
LLB     12      DIEIN   .6NH
LLE     15      DIEGND  .2NH
* Contact inductance
LL1     IN      11      .05NH
LL2     GND     14      .01NH
LL3     OUT     13      .05NH
*Parasitic capacitances
CCEB    12      15      .03PF
CCEC    15      DIEOUT  .04PF
CCBC    12      DIEOUT  .04PF
* INSERT DIE MODEL
XA05DIE DIEOUT  DIEIN   DIEGND  MSA05
.ENDS
*end Package model
*
* Start die model
.SUBCKT MSA05   DIEOUT  DIEIN   DIEGND
RF      DIEIN   13      210   
RE      Q2E     DIEGND  15    
RB      DIEIN   DIEGND  800   
RBIAS   Q1E     DIEGND  50    
RDC     DIEIN   DIEOUT  2000  
CF      DIEIN   DIEOUT  0.26PF
CP1     Q1E     DIEOUT  0.274PF
CP2     Q2E     DIEOUT  0.150PF
CP3     DIEGND  DIEOUT  0.496PF
CFBL    14      DIEOUT  45PF
LF      13      14      0.35NH
XA05Q1  DIEOUT  DIEIN   Q1E     A05Q1
XA05Q2  DIEOUT  Q1E     Q2E     A05Q2
.ENDS
* Q1 distributed base model
.SUBCKT A05Q1   Q1C     Q1B     Q1E
RB1     Q1B     11     1.26  
RB2     11      12     2.15  
RB3     12      B1     1.56  
RC      C1      Q1C    5.87  
RE      E1      13     0.14  
RBAL    13      Q1E    2.75  
Q1      C1      B1     E1      QPWR    720
DCD1    Q1B     C1     DMOD    1267
DCD2    11      C1     DMOD    2380
DCD3    12      C1     DMOD    612
DCD4    13      C1     DMOD    1717
.ENDS
* Q2 distributed base model
.SUBCKT A05Q2   Q2C     Q2B     Q2E
RB1     Q2B     21     0.91  
RB2     21      22     1.07  
RB3     22      B2     0.78  
RC      C2      Q2C    3     
RE      E2      23     0.07  
RBAL    23      Q2E    1.38  
Q1      C2      B2     E2      QPWR    1440
DCD1    Q2B     C2     DMOD    2354
DCD2    21      C2     DMOD    4760
DCD3    22      C2     DMOD    1224
DCD4    23      C2     DMOD    1717
.ENDS
.MODEL  DMOD    D(IS=1E-25, CJO=2.45E-16, VJ=.76, M=.53, BV=45, IBV=1E-9)
.MODEL  QPWR    NPN (BF=75, BR=5, IS=1.65E-18, VA=30, TF=14PS,
+               CJE=1.8E-15, VJE=1.01, MJE=0.6, PTF=50, XTB=1.818,
+               VTF=6, ITF=3E-4, IKF=1.0E-4, XTF=4, NF=1.03, ISE=5E-15,
+               NE=2.5)
*end Die model