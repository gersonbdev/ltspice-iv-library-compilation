*POLYFET RF DEVICES 
*FEB 15 1994
*PHONE:(805)484-4210; FAX:(805)484-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.55V FOR IDQ=800MA
*MODEL APPLICABLE FOR ALL F2A 4 DIE IN AP AND AK PACKAGE.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER
*		( ;D  G  S )
.SUBCKT F2048/PF  20  10  30
LG     10  11  0.85N
RGATE  11  12  0.78
CG     10  30  0.10P
CRSS   12  17  3.75P
CISS   12  14  38.54P
LS     14  30  0.10N
CS     14  30  0.70P
LD     17  20  0.88N
CD     20  30  0.10P
R_RC   16  17  28.8   
C_RC   14  16  832.7P
MOS    13  12  14  14 F2048MOS L=1.1U W=0.05       ;D G S B LEVEL1
JFET   17  14  13     F2048JF                      ;D G S
DBODY  14  17         F2048DB                      ;P N
 
.MODEL F2048MOS NMOS (VTO=2 KP=1.5E-5 LAMBDA=0.1 RD=0.6 RS=0.4)
.MODEL F2048JF  NJF  (VTO=-6.8 BETA=0.1 LAMBDA=5)
.MODEL F2048DB  D    (CJO=48P RS=0.25 VJ=0.7 M=0.35 BV=65)
.ENDS
*$