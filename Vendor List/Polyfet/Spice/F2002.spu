*POLYFET RF DEVICES 
*FEB 15 1994
*PHONE:(805)484-4210; FAX:(805)484-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.55V FOR IDQ=400MA
*MODEL APPLICABLE FOR ALL F2A 2 DIE IN AP, AK, AND CC PACKAGE.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER
*		( ;D  G  S )
.SUBCKT F2002/PF  20  10  30
LG    10  11  1.20N
RGATE 11  12  1.02
CG    10  30  0.18P
CRSS  12  17  2.80P
CISS  12  14  19.32P
LS    14  30  0.15N
CS    14  30  2.48P
LD    17  20  1.03N
CD    20  30  0.28P
R_RC  16  17  1073
C_RC  14  16  1.48P
MOS   13  12  14  14 F2002MOS L=1.1U W=0.025       ;D G S B LEVEL1
JFET  17  14  13     F2002JF                       ;D G S
DBODY 14  17         F2002DB                       ;P N
 
.MODEL F2002MOS NMOS (VTO=2 KP=1.5E-5 LAMBDA=0.1 RD=1.1 RS=1.0)
.MODEL F2002JF  NJF  (VTO=-6.8 BETA=0.1 LAMBDA=5)
.MODEL F2002DB  D    (CJO=24P RS=0.25 VJ=0.7 M=0.35 BV=65)
.ENDS
*$