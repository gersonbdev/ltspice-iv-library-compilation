*POLYFET RF DEVICES 
*OCT 20 1998
*PHONE:(805)484-4210; FAX:(805)484-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.28V FOR IDQ=606MA
*MODEL APPLICABLE FOR ALL S2A 3 DIE IN AP AND AK PACKAGE.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER
				( ;D  G  S )
.SUBCKT SP203/PF  20  10  30
LG     10  11  0.65N
RGATE  11  12  1.69
CG     10  30  1.95P
CRSS   12  17  1.87P
CISS   12  14  37.5P
LS     14  30  0.14N
CS     14  30  0.058P
LD     17  20  0.79N
CD     20  30  0.11P
R_RC   16  17  158.5
C_RC   14  16  4.3P
MOS    13  12  14  14 SP203MOS L=1.1E-6 W=0.0375   ;D G S B LEVEL1
JFET   17  14  13     SP203JF                      ;D G S
DBODY  14  17         SP203DB                      ;P N
 
.MODEL SP203MOS NMOS (VTO=2.2 KP=2.2E-5 LAMBDA=0.5 RD=0.65 RS=0.55)
.MODEL SP203JF  NJF  (VTO=-6.8 BETA=0.3 LAMBDA=1)
.MODEL SP203DB  D    (CJO=55P RS=0.25 VJ=0.7 M=0.3 BV=65)
.ENDS
*$