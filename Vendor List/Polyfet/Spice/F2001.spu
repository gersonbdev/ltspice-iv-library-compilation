*POLYFET RF DEVICES 
*FEB 15 1994
*PHONE:(805)484-4210; FAX:(805)484-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.55V FOR IDQ=200MA
*MODEL APPLICABLE FOR ALL F2A 1 DIE IN AP, AK, AQ AND CC PACKAGE.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER
*		( ;D  G  S )
.SUBCKT F2001/PF  20  10  30
LG     10  11  1.49N
RGATE  11  12  2.17
CG     10  30  0.49P
CRSS   12  17  1.06P
CISS   12  14  9.04P
LS     14  30  0.30N
CS     14  30  0.24P
LD     17  20  1.27N
CD     20  30  1.27P
R_RC   16  17  1079
C_RC   14  16  51.26P
MOS    13  12  14  14 F2001MOS L=1.1U W=0.0125     ;D G S B LEVEL1
JFET   17  14  13     F2001JF                      ;D G S
DBODY  14  17         F2001DB                      ;P N
 
.MODEL F2001MOS NMOS (VTO=2 KP=1.5E-5 LAMBDA=0.1 RD=2.9 RS=1.7)
.MODEL F2001JF  NJF  (VTO=-6.8 BETA=0.1 LAMBDA=5)
.MODEL F2001DB  D    (CJO=12P RS=0.25 VJ=0.7 M=0.35 BV=65)
.ENDS
*$