*POLYFET RF DEVICES 
*FEB 15TH 1994
*PHONE:(805)484-4210; FAX:(805)484-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.63V FOR IDQ=400MA
*MODEL APPLICABLE FOR ALL F1B 2 DIE IN AP AND AK PACKAGE.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER
*		( ;D  G  S )
.SUBCKT F1008/PF  20  10  30
LG     10  11  1.26N
RGATE  11  12  0.65
CG     10  30  0.01P
CRSS   12  17  7.5P
CISS   12  14  55.7P
LS     14  30  0.15N 
CS     14  30  0.04P
LD     17  20  0.95N
CD     20  30  0.79P
R_RC   16  17  17.4
C_RC   14  16  90P
MOS    13  12  14  14 F1008MOS L=1.1U W=0.12       ;D G S B LEVEL1
JFET   17  14  13     F1008JF                      ;D G S
DBODY  14  17         F1008DB                      ;P N
 
.MODEL F1008MOS NMOS (VTO=3 KP=1.7E-5 LAMBDA=0.1 RD=0.14 RS=0.3)
.MODEL F1008JF  NJF  (VTO=-6.8 BETA=0.2 LAMBDA=5)
.MODEL F1008DB  D    (CJO=96P RS=0.25 VJ=0.7 M=0.35 BV=65)
.ENDS
*$