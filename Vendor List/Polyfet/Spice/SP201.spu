*POLYFET RF DEVICES 
*OCT 20 1998
*PHONE:(805)484-4210; FAX:(805)484-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.25V FOR IDQ=205MA
*MODEL APPLICABLE FOR ALL S2A 1 DIE IN AP, AK, AQ AND CC PACKAGE.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER
				( ;D  G  S )
.SUBCKT SP201/PF  20  10  30
LG     10  11  1.25N
RGATE  11  12  5.27
CG     10  30  0.47P
CRSS   12  17  0.64P
CISS   12  14  12.8P
LS     14  30  0.35N
CS     14  30  0.05P
LD     17  20  0.75N
CD     20  30  0.11P
R_RC   16  17  124.5
C_RC   14  16  1.7P
MOS    13  12  14  14 SP201MOS L=1.1E-6 W=0.0125     ;D G S B LEVEL1
JFET   17  14  13     SP201JF                      ;D G S
DBODY  14  17         SP201DB                      ;P N
 
.MODEL SP201MOS NMOS (VTO=2.2 KP=2.2E-5 LAMBDA=0.5 RD=2.2 RS=1.9)
.MODEL SP201JF  NJF  (VTO=-6.8 BETA=0.3 LAMBDA=1)
.MODEL SP201DB  D    (CJO=19P RS=0.25 VJ=0.7 M=0.3 BV=65)
.ENDS
*$