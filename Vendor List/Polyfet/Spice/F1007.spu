*POLYFET RF DEVICES 
*FEB 15TH 1994
*PHONE:(805)484-4210; FAX:(805)494-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.63V FOR IDQ=200MA
*MODEL APPLICABLE FOR ALL F1B 1 DIE IN AP, AK, AQ AND CC PACKAGE.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER
*		( ;D  G  S )
.SUBCKT F1007/PF  20  10  30
LG     10  11  1.71n
RGATE  11  12  0.78
CG     10  30  0.05P
CRSS   12  17  4.09P
CISS   12  14  29.03P
LS     14  30  0.30N 
CS     14  30  0.10P
LD     17  20  0.85N
CD     20  30  1.44P
R_RC   16  17  35.73
C_RC   14  16  11.8P
MOS    13  12  14  14 F1007MOS L=1.1U W=0.06       ;D G S B LEVEL1
JFET   17  14  13     F1007JF                      ;D G S
DBODY  14  17         F1007DB                      ;P N
 
.MODEL F1007MOS NMOS (VTO=3 KP=1.7E-5 LAMBDA=0.1 RD=0.28 RS=0.6)
.MODEL F1007JF  NJF  (VTO=-6.8 BETA=0.2 LAMBDA=5)
.MODEL F1007DB  D    (CJO=48P RS=0.25 VJ=0.7 M=0.35 BV=65)
.ENDS
*$