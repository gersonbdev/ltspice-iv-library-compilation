*POLYFET RF DEVICES
*FEB 15 1994
*PHONE:(805)484-4210; FAX:(805)484-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.63V FOR IDQ=600MA
*MODEL APPLICABLE FOR ALL F1B 3 DIE IN AP, AK, AQ AND CC PACKAGE.  F1022 USE
*BELOW AS EXAMPLE.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER

*		( ;D  G  S )
.SUBCKT F1022/PF  20  10  30
LG    10  11  1.0N
RGATE 11  12  0.39
CG    10  30  1.0P
CRSS  12  17  10.9P
CISS  12  14   84P
LS    14  30  0.11N
CS    14  30  2.0P
LD    17  20  0.85N
CD    20  30  1.12P
R_RC  16  17  11.2
C_RC  14  16  80.0P
MOS   13  12  14  14  F1022MOS L=1.1U W=0.18       ;D G S B LEVEL1
JFET  17  14  13      F1022JF                      ;D G S
DBODY 14  17          F1022DB                      ;P N

.MODEL F1022MOS NMOS (VTO=3 KP=1.7E-5 LAMBDA=0.1 RD=0.1 RS=0.17)
.MODEL F1022JF  NJF  (VTO=-6.8 BETA=0.2 LAMBDA=5)
.MODEL F1022DB  D    (CJO=144P RS=0.25 VJ=0.7 M=0.35 BV=65)
.ENDS
*$