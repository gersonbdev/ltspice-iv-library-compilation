S*POLYFET RF DEVICES 
*MAY 7TH 2001
*PHONE:(805)484-4210; FAX:(805)494-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=4.3V FOR IDQ=455MA
*MODEL APPLICABLE FOR SM341.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER
*		( ;D  G  S )
.SUBCKT SM341/PF  20  10  30
LG     10  11  0.29n
RGATE  11  12  0.4
CG     10  30  0.01P
CRSS   12  17  11.9P
CISS   12  14  448P
LS     14  30  0.5N 
CS     14  30  0.02P
LD     17  20  2.2N
CD     20  30  0.02P
R_RC   16  17  17.7
C_RC   14  16  53.3P
MOS    13  12  14  14 SM341MOS L=1.1U W=0.778      ;D G S B LEVEL1
JFET   17  14  13     SM341JF                      ;D G S
DBODY  14  17         SM341DB                      ;P N
 
.MODEL SM341MOS NMOS (VTO=4 KP=0.9E-5 LAMBDA=0.15 RD=0.065 RS=0.07)
.MODEL SM341JF  NJF  (VTO=-6.8 BETA=1 LAMBDA=1)
.MODEL SM341DB  D    (CJO=1100P RS=0.25 VJ=0.6 M=0.4 BV=130)
.ENDS
*$