*POLYFET RF DEVICES 
*OCT 20 1998
*PHONE:(805)484-4210; FAX:(805)484-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.25V FOR IDQ=427MA
*MODEL APPLICABLE FOR ALL S2A 2 DIE IN AP, AK, CC PACKAGE.
* VG=3.2V; ID=400MA
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER
				( ;D  G  S )
.SUBCKT SQ202/PF  20  10  30
LG     10  11  1.0N
RGATE  11  12  2.56
CG     10  30  1.54P
CRSS   12  17  1.29P
CISS   12  14  23.9P
LS     14  30  0.29N
CS     14  30  0.023P
LD     17  20  0.54N
CD     20  30  0.1P
R_RC   16  17  130.7
C_RC   14  16  0.74P
MOS    13  12  14  14 SQ202MOS L=1.1E-6 W=0.025     ;D G S B LEVEL1
JFET   17  14  13     SQ202JF                      ;D G S
DBODY  14  17         SQ202DB                      ;P N
 
.MODEL SQ202MOS NMOS (VTO=2.2 KP=2.2E-5 LAMBDA=0.5 RD=1.0 RS=0.9)
.MODEL SQ202JF  NJF  (VTO=-6.8 BETA=0.3 LAMBDA=1)
.MODEL SQ202DB  D    (CJO=36P RS=0.25 VJ=0.7 M=0.3 BV=65)
.ENDS
*$