*POLYFET RF DEVICES
*FEB 15 1994
*PHONE:(805)484-4210; FAX:(805)494-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.58V FOR
*IDQ=800MA

*MODEL APPLICABLE FOR ALL F1B 4 DIE IN AP AND AK PACKAGE.  F1021 USE
*BELOW AS EXAMPLE.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER

*		( ;D  G  S )
.SUBCKT F1021/PF  20  10  30
LG    10  11  0.87N
RGATE 11  12  0.61
CG    10  30  0.12P
CRSS  12  17  14.2P
CISS  12  14  130P
LS    14  30  0.09N
CS    14  30  0.06P
LD    17  20  0.75N
CD    20  30  0.45P
R_RC  16  17   8.2
C_RC  14  16   61P
MOS   13  12  14  14  F1021MOS L=1.1U W=0.24       ;D G S B LEVEL1
JFET  17  14  13      F1021JF                      ;D G S
DBODY 14  17          F1021DB                      ;P N

.MODEL F1021MOS NMOS (VTO=3 KP=1.7E-5 LAMBDA=0.1 RD=0.1 RS=0.09)
.MODEL F1021JF  NJF  (VTO=-6.8 BETA=0.2 LAMBDA=5)
.MODEL F1021DB  D    (CJO=192P RS=0.25 VJ=0.7 M=0.35 BV=65)
.ENDS
*$