*POLYFET RF DEVICES 
*FEB 15 1994
*PHONE:(805)484-4210; FAX:(805)484-3393  CONTACT:MR. S.K. LEONG
*HIGH POWER, HIGH FREQUENCY, RF N-CHANNEL VERTICAL DMOS MOSFET
*TO GENERATE S PARAMETER MATCHING DATA SHEET, SET VG=3.55V FOR IDQ=600MA
*MODEL APPLICABLE FOR ALL F2A 3 DIE IN AP AND AK PACKAGE.
*  NOTE:- HP/EESOF USES 'GATE DRAIN SOURCE' ORDER
*		( ;D  G  S )
.SUBCKT F2021/PF  20  10  30
LG     10  11  1.12N
RGATE  11  12  0.98
CG     10  30  0.01P
CRSS   12  17  2.88P
CISS   12  14  32.00P
LS     14  30  0.13N
CS     14  30  1.20P
LD     17  20  0.89N
CD     20  30  0.62P
R_RC   16  17  1382
C_RC   14  16  32.6P
MOS    13  12  14  14 F2021MOS L=1.1U W=0.0375         ;D G S B LEVEL1
JFET   17  14  13     F2021JF                          ;D G S
DBODY  14  17         F2021DB                          ;P N
 
.MODEL F2021MOS NMOS (VTO=2 KP=1.5E-5 LAMBDA=0.1 RD=0.7 RS=0.6)
.MODEL F2021JF  NJF  (VTO=-6.8 BETA=0.1 LAMBDA=5)
.MODEL F2021DB  D    (CJO=36P RS=0.25 VJ=0.7 M=0.35 BV=65)
.ENDS
*$