*SPICE model for MSA0736
*Start Package model 
.SUBCKT MSA0736   OUT   IN   GND
* PI NETWORK TO SIMULATE TRANSMISSION LINE T1
C1T1    11      0       .061PF
LT1     11      12      .507NH
C2T1    12      0       .061PF
* PI NETWORK TO SIMULATE TRANSMISSION LINE T2
C1T2    14      0       .055PF
LT2     14      15      .098NH
C2T2    15      0       .055PF
* PI NETWORK TO SIMULATE TRANSMISSION LINE T3
 C1T3    13      0       .087PF
LT3     13      DIEOUT  .721NH
C2T3    DIEOUT  0       .087PF
* Bond wire inductance
LLB     12      DIEIN   .6NH
LLE     15      DIEGND  .3NH
* Contact inductance
LL1     IN      11      .05NH
LL2     GND     14      .01NH
LL3     OUT     13      .05NH
*Parasitic capacitances
CCEB    12      15      .01PF
CCEC    15      DIEOUT  .03PF
CCBC    12      DIEOUT  .02PF
* INSERT DIE MODEL
XA07DIE DIEOUT  DIEIN   DIEGND  MSA07
.ENDS
*end Package model
*
* Start die model
.SUBCKT MSA07   DIEOUT  DIEIN   DIEGND
RF      DIEIN   DIEOUT  360   
RB      DIEIN   DIEGND  244   
RBIAS   Q1E     DIEGND  215   
RE      Q2E     DIEGND  6     
CF      DIEIN   DIEOUT  0.067PF
CP1     Q1E     DIEOUT  0.020PF
CP2     Q2E     DIEOUT  0.045PF
CP3     DIEGND  DIEOUT  0.104PF
XA07Q1  DIEOUT  DIEIN   Q1E     A07Q1
XA07Q2  DIEOUT  Q1E     Q2E     A07Q2
.ENDS
* Q1 distributed base model
.SUBCKT A07Q1   Q1C     Q1B     Q1E
RB1     Q1B     11     1.5   
RB2     11      12     4.4   
RB3     12      B1     3.8   
RE      E1      Q1E    0.33  
RC      C1      Q1C    10    
Q1      C1      B1     E1      QMSA    300
DCD1    Q1B     C1     DMOD    602
DCD2    11      C1     DMOD    450
DCD3    12      C1     DMOD    261
.ENDS
* Q2 distributed base model
.SUBCKT A07Q2   Q2C     Q2B     Q2E
RB1     Q2B     21     1.2   
RB2     21      22     3.1   
RB3     22      B2     2.7   
RE      E2      Q2E    0.24  
RC      C2      Q2C    10    
Q1      C2      B2     E2      QMSA   420
DCD1    Q2B     C2     DMOD    782
DCD2    21      C2     DMOD    629
DCD3    22      C2     DMOD    365
.ENDS
* DIODE AND TRANSISTOR MODELS USED IN AGILENT TECHNOLOGIES MMICS AND DISCRETES
.MODEL  DMOD    D(IS=1E-25, CJO=2.45E-16, VJ=.76, M=.53, BV=45, IBV=1E-9)
.MODEL  QMSA    NPN (BF=90, BR=5, IS=1.65E-18, VA=20, TF=12PS,
+               CJE=1.8E-15, VJE=1.01, MJE=0.6, PTF=25, XTB=1.818,
+               VTF=6, ITF=3E-4, IKF=1.333E-4, XTF=4, NF=1.03, ISE=5E-15,
+               NE=2.5)
*End Die model