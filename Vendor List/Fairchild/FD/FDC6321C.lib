* FDC6321C ELECTRICAL MODEL (PRELIMINARY) (SuperSOT-6 Complementary N-Ch DMOS)
* --------------------------------------------------------------
.SUBCKT  FDC6321CN 20  10  30
Rg     10    1    1
M1      2    1    3    3    DMOS    L=1u   W=1u
.MODEL DMOS NMOS (VTO={0.9*(-0.00232*TEMP+1.058)}  KP={-0.0005*TEMP+2.5}  THETA=0.096
+ VMAX=VMAX=3.8E5  LEVEL=3)
Cgs     1    3    200p
Rd     20    4    .12 TC=0.004
Dds     3    4    DDS
.MODEL     DDS    D(BV={25*(0.001*TEMP+0.975)}   M=0.36  CJO=23p   VJ=0.8)
Dbody   3   20    DBODY
.MODEL   DBODY    D(IS=1.4E-13   N=1   RS=40m   TT=100n)
Ra      4    2    .12 TC=0.004
Rs      3    5    10m
Ls      5    30   .7n
M2      1    8    6    6   INTER
E2      8    6    4    1   2
.MODEL   INTER    NMOS(VTO=0  KP=10   LEVEL=1)
Cgdmax  7    4    140p
Rcgd    7    4    10meg
Dgd     6    4    DGD
Rdgd    4    6    10meg
.MODEL     DGD    D(M=0.65   CJO=140p   VJ=0.35)
M3      7    9    1    1   INTER
E3      9    1    4    1   -2
.ENDS FDC6321CN
*
* FDC6321CP ELECTRICAL MODEL (PRELIMINARY) (SuperSOT-6 Complementary P-Ch DMOS)
* ---------------------------------------------------------------
.SUBCKT  FDC6321CP  20  10  30
Rg     10    1    1
M1      2    1    3    3    DMOS    L=1u   W=1u
.MODEL DMOS PMOS (VTO={-0.8*(-0.00232*TEMP+1.058)}  KP={-0.0009*TEMP+0.8225}
+   THETA=0.096 VMAX=3.8E5  LEVEL=3)
Cgs     1    3    130p
Rd     20    4    0.25 TC=0.004
Dds     4    3    DDS
.MODEL     DDS    D(BV={25*(0.00088*TEMP+0.978)}   M=0.35  CJO=100p   VJ=1.0)
Dbody   20   3    DBODY
.MODEL   DBODY    D(IS=1E-12   N=1   RS=12.7m   TT=25n)
Ra      4    2    0.25 TC=0.004
Rs      3    5    10m
Ls      5    30   0.5n
M2      1    8    6    6   INTER
E2      8    6    4    1   2
.MODEL   INTER    PMOS(VTO=0  KP=10   LEVEL=1)
Cgdmax  7    4    110p
Rcgd    7    4    10meg
Dgd     4    6    DGD
Rdgd    4    6    10meg
.MODEL     DGD    D(M=0.55   CJO=110p   VJ=0.4)
M3      7    9    1    1   INTER
E3      9    1    4    1   -2
.ENDS FDC6321CP
