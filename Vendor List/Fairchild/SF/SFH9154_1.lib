* SFH9154 ELECTRICAL MODEL (TO-3P N-Ch DMOS)
* ----------------------------------------------------
* NOTICE : This SPICE data is verified for -55 ~ 100C
* ----------------------------------------------------
.SUBCKT  SFH9154  20  10  30
Rg     10    1    1
M1      2    1    3    3    DMOS    L=1u   W=1u
.MODEL DMOS PMOS (VTO={-2.4*{-0.0008*TEMP+1.02}} 
+  KP={11*{-0.0001*TEMP+1.0025}}
+  THETA=0.040  VMAX=5.2E4  LEVEL=3 )
Cgs     1    3    1800p
Rd     20    4    120m TC=0.00267
Dds     4    3    DDS
.MODEL     DDS    D(BV={150*{0.00045*TEMP+0.98875}}   
+ M=0.65  CJO=280p   VJ=0.75*{-0.0022*TEMP+0.855})
Dbody   20   3    DBODY
.MODEL   DBODY    D(IS=1E-13   N=0.8  RS=600m  EG=1.12  TT=200n)
Ra      4    2    60m TC=0.00267
Rs      3    5    3m
Ls      5    30   0.3n
M2      1    8    6    6   INTER
E2      8    6    4    1   2
.MODEL   INTER    PMOS(VTO=0  KP=10   LEVEL=1)
Cgdmax  7    4    3250p
Rcgd    7    4    10meg
Dgd     4    6    DGD
Rdgd    4    6    10meg
.MODEL     DGD    D(M=0.68   CJO=3250p   VJ=0.99)
M3      7    9    1    1   INTER
E3      9    1    4    1   -2
.ENDS SFH9154
