* FQP13N06L ELECTRICAL MODEL
* PACKAGE TYPE : TO-220
* DEVICE TYPE : N-CH DMOS
* DATE : 2000-01-20
* Made by HW Jang ( contact 82-32-680-1249 )
* ----------------------------------------------------------------------------
.SUBCKT  FQP13N06L  20  10  30
Rg     10    1    1
M1      2    1    3    3    DMOS    L=1u   W=1u
.MODEL DMOS NMOS (VTO={1.7*{-0.001*TEMP+1.025}}  KP={13*{-0.0004*TEMP+1.01}}
+  THETA=0.08   VMAX=4.0E4   LEVEL=3)
Cgs     1    3    273p
Rd     20    4    35m TC=0.0073
Dds     3    4    DDS
.MODEL     DDS    D(BV={60*{0.0008*TEMP+0.98}}   M=0.5  CJO=78p  
+  VJ=0.8*{-0.0022*TEMP+1.055})
Dbody   3   20    DBODY
.MODEL DBODY  D(IS=4.0E-13   N=1.01   RS=11m   EG=1.18    TT=52n)
Ra      4    2    33.3m TC=0.0073
Rs      3    5    2.6m
Ls      5    30   2.5n
M2      1    8    6    6   INTER
E2      8    6    4    1   2
.MODEL   INTER    NMOS(VTO=0  KP=10   LEVEL=1)
Cgdmax  7    4    312p
Rcgd    7    4    10meg
Dgd     6    4    DGD
Rdgd    4    6    10meg
.MODEL     DGD    D(M=0.55   CJO=312p   VJ=0.45)
M3      7    9    1    1   INTER
E3      9    1    4    1   -2
.ENDS FQP13N06L
* -----------------------------------------------------------------------------
* Fairchild Korea Semiconductor