*
* Linear Technology LT1115 op amp model
* Written: 11-28-1989 Type: Bipolar npn input, internal comp.
* Typical specs: 
*       Ref. LT1115 data sheet, LTC Dec 1989
* Comments:
*       Uses extended phase compensation; input differential mode clamp.
*
* Connections: + - V+V-O 
.SUBCKT LT1115 3 2 7 4 6
RC1 7 80 7.0736E+01 
RC2 7 90 7.0736E+01 
Q1 80 2 10 QM1 
Q2 90 3 11 QM2 
*
C1 80 91 750E-12 
RXC1 91 90 50 
CXC1 91 90 400E-12 
C2 1 98 30.000E-12 
RXC2 98 8 1K 
CXC2 98 8 10.000E-12 
*
CIN 2 3 15E-12 
RIN 2 3 2E4 
DDM1 2 104 DM2
DDM3 104 3 DM2
DDM2 3 105 DM2
DDM4 105 2 DM2
RE1 10 12 -4.4157E+01 
RE2 11 12 -4.4157E+01 
IEE 12 4 4.5006E-04
RE 12 0 4.4439E+05
CE 12 0 1.5789E-12
GCM 0 8 12 0 7.0854E-09
GA 8 0 80 90 1.4137E-02 
R2 8 0 1.0000E+05
GB 1 0 8 0 2.6731E+02 
RO2 1 0 7.9000E+01
* 
RS 1 6 1 
ECL 18 0 1 6 2.7910E+01
GCL 0 8 20 0 1
RCL 20 0 1E3
D1 18 20 DM1 
D2 20 18 DM1 
* 
D3A 131 70 DM3 
D3B 13 131 DM3 
GPL 0 8 70 7 1 
VC 13 6 3.6394
RPLA 7 70 1E4 
RPLB 7 131 1E5 
D4A 60 141 DM3
D4B 141 14 DM3
GNL 0 8 60 4 1 
VE 6 14 3.6394
RNLA 60 4 1E4 
RNLB 141 4 1E5 
*
IP 7 4 7.450E-03
DSUB 4 7 DM2 
* MODELS
.MODEL QM1 NPN (IS=8.0000E-16 BF=3.4615E+03)
.MODEL QM2 NPN (IS=8.0155E-16 BF=6.4286E+03)
.MODEL DM1 D (IS=1.000E-19)
.MODEL DM2 D (IS=8.000E-16)
.MODEL DM3 D (IS=1.000E-20)
.ENDS LT1115
*
* - - - - - * FINI LT1115 * - - - - - *
***
*         (C) COPYRIGHT LINEAR TECHNOLOGY CORPORATION 1990
*                       All rights reserved.
* 
*   Linear Technology Corporation hereby grants the users of this
* macromodel a non-exclusive, nontransferrable license to use this
*            macromodel under the following conditions:
* 
* The user agrees that this macromodel is licensed from Linear
* Technology and agrees that the macromodel may be used, loaned,
* given away or included in other model libraries as long as this
* notice and the model in its entirety and unchanged is included.
* No right to make derivative works or modifications to the
* macromodel is granted hereby.  All such rights are reserved.
* 
* This model is provided as is.  Linear Technology makes no
* warranty, either expressed or implied about the suitability or
* fitness of this model for any particular purpose.  In no event
* will Linear Technology be liable for special, collateral,
* incidental or consequential damages in connection with or arising
* out of the use of this macromodel.  It should be remembered that
* models are a simplification of the actual circuit.
* 
* Linear Technology reserves the right to change these macromodels
* without prior notice.  Contact Linear Technology at 1630 McCarthy
* Blvd., Milpitas, CA, 95035-7487 or telephone 408/432-1900 for
* datasheets on the actual amplifiers or the latest macromodels.
* 
* ----------------------------------------------------------------------- *
